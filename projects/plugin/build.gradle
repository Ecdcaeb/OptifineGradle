plugins {
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.6'
}

java.toolchain.setLanguageVersion(JavaLanguageVersion.of(21))

base {
    group = 'io.github.ecdcaeb.gradle.optifine'
    version =  "0.1"
}

repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
    maven {
        url = "https://maven.wagyourtail.xyz/releases"
    }
    maven {
        url = "https://maven.wagyourtail.xyz/snapshots"
    }
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    compileOnly(gradleApi())

    shade("org.ow2.asm:asm:9.8")
    shade("org.ow2.asm:asm-commons:9.8")
    shade("org.vineflower:vineflower:1.11.1")
    shade("net.neoforged:AutoRenamingTool:2.0.13")

    implementation("xyz.wagyourtail.unimined:unimined:1.4.1")
    implementation("xyz.wagyourtail.unimined.mapping:unimined-mapping-library-jvm:1.2.1")
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.shade]
    //relocate 'org.objectweb.asm', 'io.github.ecdcaeb.gradle.optifine.lib.asm'
}

gradlePlugin {
    plugins {
        optifine {
            id = 'io.github.ecdcaeb.gradle.optifine'
            implementationClass = 'io.github.ecdcaeb.gradle.optifine.OptifinePlugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = "ASMImplementPlugin"
            version = project.version
        }
    }

    repositories {
        if (project.hasProperty("publish-username")) {
            maven {
                url project.hasProperty("publish-maven") ? project.getProperty("publish-maven") : ""
                credentials {
                    username project.getProperty("publish-username")
                    password project.getProperty("publish-password")
                }
            }
        }
        maven {
            url layout.buildDirectory.dir("maven")
        }
    }
}

sourceSets {

}